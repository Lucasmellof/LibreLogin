plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'net.kyori.blossom' version '1.3.0'
    id 'java-library'
}

version '0.14.0'

compileJava {
    options.encoding('UTF-8')
}

repositories {
    mavenLocal()
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://repo.opencollab.dev/maven-snapshots/" }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://hub.spigotmc.org/nexus/" }
    maven { url = "https://mvn.exceptionflug.de/repository/exceptionflug-public/" }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://repo.dmulloy2.net/repository/public/" }
    maven { url = "https://repo.alessiodp.com/releases/" }
}

blossom {
    replaceToken '@version@', version
}

shadowJar {
    archiveFileName = "LibreLogin.jar"

    dependencies {
        exclude(dependency('org.slf4j:.*:.*'))
        exclude(dependency('org.checkerframework:.*:.*'))
        exclude(dependency('com.google.errorprone:.*:.*'))
        exclude(dependency('com.google.protobuf:.*:.*'))
    }

    relocate 'co.aikar.acf', 'xyz.kyngs.librelogin.lib.acf'
    relocate 'com.github.benmanes.caffeine', 'xyz.kyngs.librelogin.lib.caffeine'
    relocate 'com.typesafe.config', 'xyz.kyngs.librelogin.lib.hocon'
    relocate 'com.zaxxer.hikari', 'xyz.kyngs.librelogin.lib.hikari'
    relocate 'com.mysql', 'xyz.kyngs.librelogin.lib.mysql'
    relocate 'org.intellij', 'xyz.kyngs.librelogin.lib.intellij'
    relocate 'org.jetbrains', 'xyz.kyngs.librelogin.lib.jetbrains'
    relocate 'io.leangen.geantyref', 'xyz.kyngs.librelogin.lib.reflect'
    relocate 'org.spongepowered.configurate', 'xyz.kyngs.librelogin.lib.configurate'
    relocate 'net.byteflux.libby', 'xyz.kyngs.librelogin.lib.libby'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    from '../LICENSE.txt'
}


dependencies {
    //Velocity
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    compileOnly 'com.velocitypowered:velocity-proxy:3.0.1'

    //MySQL
    compileOnly 'mysql:mysql-connector-java:8.0.30'
    compileOnly 'com.zaxxer:HikariCP:5.0.1'

    //SQLite
    compileOnly 'org.xerial:sqlite-jdbc:3.40.1.0'

    //ACF
    implementation 'com.github.kyngs.commands:acf-velocity:object-fix-SNAPSHOT'
    implementation 'com.github.kyngs.commands:acf-bungee:object-fix-SNAPSHOT'
    implementation 'com.github.kyngs.commands:acf-paper:object-fix-SNAPSHOT'

    //Utils
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.1'
    implementation 'org.spongepowered:configurate-hocon:4.1.2'
    implementation 'at.favre.lib:bcrypt:0.9.0'
    implementation 'dev.samstevens.totp:totp:1.7.1'
    implementation 'dev.simplix:protocolize-api:2.2.5'

    //Geyser
    compileOnly 'org.geysermc.floodgate:api:2.0-SNAPSHOT'

    //Bungeecord
    compileOnly 'net.md-5:bungeecord-api:1.18-R0.1-SNAPSHOT'
    implementation 'net.kyori:adventure-platform-bungeecord:4.1.2'
    compileOnly 'com.github.limework.redisbungee:RedisBungee-API:0.7.2'

//    //BStats
//    implementation 'org.bstats:bstats-velocity:3.0.0'
//    implementation 'org.bstats:bstats-bungeecord:3.0.0'
//    implementation 'org.bstats:bstats-bukkit:3.0.0'

    //Paper
    compileOnly 'io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT'
//    compileOnly 'com.comphenix.protocol:ProtocolLib:5.0.0-SNAPSHOT'
    compileOnly(files('libs/ProtocolLib.jar'))
    compileOnly 'io.netty:netty-transport:4.1.79.Final'
    compileOnly 'com.mojang:datafixerupper:5.0.28' //I hate this so much
    compileOnly 'org.apache.logging.log4j:log4j-core:2.19.0'

    //Libby
    implementation 'net.byteflux:libby-bukkit:1.1.5'
    implementation 'net.byteflux:libby-velocity:1.1.5'
    implementation 'net.byteflux:libby-bungee:1.1.5'

}

processResources {
    outputs.upToDateWhen { false }
    filesMatching('plugin.yml') {
        expand 'version': version
    }
    filesMatching('bungee.yml') {
        expand 'version': version
    }
}
